Program = _{ SOI ~ Expr ~ EOF }

// Expressions
Expr = { BinaryExpr | UnaryExpr | Leaf }
Term = _{ Leaf | UnaryExpr | "(" ~ Expr ~ ")"}
UnaryExpr = { Operator ~ Term }
BinaryExpr = { Term ~ (Operator ~ Term)* }

// Operators
NumericOperator = _{ "+" | "-" }
BoolOperator = _{"and" | "or" | "not"}
Comparator = _{"==" | "!=" | "<=" | "<" | ">=" | ">" }
Operator = { NumericOperator | BoolOperator | Comparator }

// Strings
UnsafeStringChar = _{"\"" | "(" | ")" | NEWLINE}
NonQuoteWhitespaceChar = _{!(UnsafeStringChar | WHITESPACE) ~ ANY}
NonQuoteNewlineChar = _{!UnsafeStringChar ~ ANY}
UnquotedString = _{ !Operator ~ NonQuoteWhitespaceChar+ }
String = @{ NonQuoteNewlineChar* }
QuotedString = _{"\"" ~ String ~ "\""}

// Values
Number = {NumericOperator? ~ ASCII_DIGIT+}
Bool = _{ "true" | "false" }
Value = @{ Number | Bool | UnquotedString }

// Variables
VariableRef = _{ "$" ~ Variable }
Variable = @{ UnquotedString }

Leaf = _{QuotedString | VariableRef | Value}

// Override builtins
WHITESPACE = _{ " " | "\t" }
EOF = _{ EOI | ";" }